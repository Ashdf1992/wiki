protocol WFPCapture with 
OPNAuthoring
{
    Copyright = "(c) Microsoft Corporation",
    References = 
        [
        ],
    RevisionSummary = 
        [
            new Revision{Class = RevisionClass.Major, Version = "374743", Date = "02/15/2015"}
        ]
};

using Etw;
using EtwEvent;
using Utility;
using IANA;
using ERREF;
using WfpUtility;
using InfrastructureResources;

public const guid EtwProviderIDWfpCapture = {c22d1b14-c242-49de-9f17-1d76b8b9c458};

endpoint WFPFrame[binary LocalAddress, binary RemoteAddress]
    accepts MessageV4
    accepts MessageV6
    accepts Message2V4
    accepts Message2V6
    accepts AuthMessageV4
    accepts AuthMessageV6
    accepts DriverInfo
    accepts CalloutInfo
    accepts CalloutNotifyInfo
    accepts DriverError
    accepts CalloutError
    accepts Discard;

autostart actor WfpOverEtwEvent(EtwEvent.Node etw)
{
    process etw accepts etwMsg:EtwProviderMsg
        where etwMsg.EventRecord.Header.ProviderId == EtwProviderIDWfpCapture
    {
        binary localAddr;
        binary remoteAddr;
        switch (etwMsg.EventRecord.Header.Descriptor.Id) // event ID
        {
            // Diagnostic driver operation events

            // DriverLoad
            case 10001 =>
                DriverInfo msg;
                DecodeBinaryWithErrorOnFail<DriverInfo>(etwMsg.Payload, "WfpCapture", "DriverInfo", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // DriverUnload
            case 10002 =>
                DriverInfo msg;
                DecodeBinaryWithErrorOnFail<DriverInfo>(etwMsg.Payload, "WfpCapture", "DriverInfo", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // CalloutRegister
            case 10003 =>
                CalloutInfo msg;
                DecodeBinaryWithErrorOnFail<CalloutInfo>(etwMsg.Payload, "WfpCapture", "CalloutInfo", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // CalloutUnregister
            case 10004 =>
                CalloutInfo msg;
                DecodeBinaryWithErrorOnFail<CalloutInfo>(etwMsg.Payload, "WfpCapture", "CalloutInfo", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // CalloutNotifyFilterAdd
            case 10005 =>
                CalloutNotifyInfo msg;
                DecodeBinaryWithErrorOnFail<CalloutNotifyInfo>(etwMsg.Payload, "WfpCapture", "CalloutNotifyInfo", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // CalloutNotifyFilterDelete
            case 10006 =>
                CalloutNotifyInfo msg;
                DecodeBinaryWithErrorOnFail<CalloutNotifyInfo>(etwMsg.Payload, "WfpCapture", "CalloutNotifyInfo", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // Diagnostic driver error events

            // DriverLoadError
            case 20001 =>
                DriverError msg;
                DecodeBinaryWithErrorOnFail<DriverError>(etwMsg.Payload, "WfpCapture", "DriverError", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // DriverUnloadError
            case 20002 =>
                DriverError msg;
                DecodeBinaryWithErrorOnFail<DriverError>(etwMsg.Payload, "WfpCapture", "DriverError", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // CalloutRegisterError
            case 20003 =>
                CalloutError msg;
                DecodeBinaryWithErrorOnFail<CalloutError>(etwMsg.Payload, "WfpCapture", "CalloutError", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // CalloutUnregisterError
            case 20004 =>
                CalloutError msg;
                DecodeBinaryWithErrorOnFail<CalloutError>(etwMsg.Payload, "WfpCapture", "CalloutError", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // CalloutClassifyError
            case 20005 =>
                CalloutError msg;
                DecodeBinaryWithErrorOnFail<CalloutError>(etwMsg.Payload, "WfpCapture", "CalloutError", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            // Message Trace Events

            // TransportMessageV4
            case 60011 =>
                MessageV4 msg;
                DecodeBinaryWithErrorOnFail<MessageV4>(etwMsg.Payload, "WfpCapture", "MessageV4", ref msg);
                GetLocalAndRemoteAddress(msg.DestinationAddress.FlatAddress, msg.SourceAddress.FlatAddress, out localAddr, out remoteAddr);
                dispatch (endpoint WFPFrame[localAddr, remoteAddr]) accepts msg;

            // TransportMessage2V4
            case 60012 =>
                Message2V4 msg;
                DecodeBinaryWithErrorOnFail<Message2V4>(etwMsg.Payload, "WfpCapture", "Message2V4", ref msg);
                GetLocalAndRemoteAddress(msg.DestinationAddress.FlatAddress, msg.SourceAddress.FlatAddress, out localAddr, out remoteAddr);
                dispatch (endpoint WFPFrame[localAddr, remoteAddr]) accepts msg;

            // TransportMessageV6
            case 60021 =>
                MessageV6 msg;
                DecodeBinaryWithErrorOnFail<MessageV6>(etwMsg.Payload, "WfpCapture", "MessageV6", ref msg);
                GetLocalAndRemoteAddress(msg.DestinationAddress.FlatAddress, msg.SourceAddress.FlatAddress, out localAddr, out remoteAddr);
                dispatch (endpoint WFPFrame[localAddr, remoteAddr]) accepts msg;

            // TransportMessage2V6
            case 60022 =>
                Message2V6 msg;
                DecodeBinaryWithErrorOnFail<Message2V6>(etwMsg.Payload, "WfpCapture", "Message2V6", ref msg);
                GetLocalAndRemoteAddress(msg.DestinationAddress.FlatAddress, msg.SourceAddress.FlatAddress, out localAddr, out remoteAddr);
                dispatch (endpoint WFPFrame[localAddr, remoteAddr]) accepts msg;

            // AleAuthMessageV4
            case 60031 =>
                AuthMessageV4 msg;
                DecodeBinaryWithErrorOnFail<AuthMessageV4>(etwMsg.Payload, "WfpCapture", "AuthMessageV4", ref msg);
                GetLocalAndRemoteAddress(msg.DestinationAddress.FlatAddress, msg.SourceAddress.FlatAddress, out localAddr, out remoteAddr);
                dispatch (endpoint WFPFrame[localAddr, remoteAddr]) accepts msg;

            // AleAuthMessageV6
            case 60041 =>
                AuthMessageV6 msg;
                DecodeBinaryWithErrorOnFail<AuthMessageV6>(etwMsg.Payload, "WfpCapture", "AuthMessageV6", ref msg);
                GetLocalAndRemoteAddress(msg.DestinationAddress.FlatAddress, msg.SourceAddress.FlatAddress, out localAddr, out remoteAddr);
                dispatch (endpoint WFPFrame[localAddr, remoteAddr]) accepts msg;

            // Discard
            case 60050 =>
                Discard msg;
                DecodeBinaryWithErrorOnFail<Discard>(etwMsg.Payload, "WfpCapture", "Discard", ref msg);
                dispatch endpoint WFPFrame[$[], $[]] accepts msg;

            default =>
                ThrowDecodingException("WFPCapture", UTILITY_INVALID_DATA, ["event ID"]);
        }
    }
}

void GetLocalAndRemoteAddress(binary addr1, binary addr2, out binary localAddr, out binary remoteAddr)
{
    if (addr1 < addr2)
    {
        localAddr = addr1;
        remoteAddr = addr2;
    }
    else
    {
        localAddr = addr2;
        remoteAddr = addr1;
    }
}

message MessageV4
{
    IPv4Address   SourceAddress with Visualization{AliasName = "Address"};
    IPv4Address   DestinationAddress with Visualization{AliasName = "Address"};
    byte   Protocol;
    short  PayloadLength;
    binary Payload;
    
    override string ToString()
    {
        return "MessageV4, Protocol: " + EnumToStringInSummary<IANA.ProtocolType>(Protocol);
    }
}

message MessageV6
{
    IPv6Address SourceAddress with Visualization{AliasName = "Address"};
    IPv6Address DestinationAddress with Visualization{AliasName = "Address"};
    byte Protocol;
    short PayloadLength;
    binary Payload;
    
    override string ToString()
    {
        return "MessageV6, Protocol: " + EnumToStringInSummary<IANA.ProtocolType>(Protocol);
    }
}

message Message2V4
{
    IPv4Address   SourceAddress with Visualization{AliasName = "Address"};
    IPv4Address   DestinationAddress with Visualization{AliasName = "Address"};
    byte   Protocol;
    long FlowContext;
    short  PayloadLength;
    binary Payload;
    
    override string ToString()
    {
        return "Message2V4, Protocol: " + EnumToStringInSummary<IANA.ProtocolType>(Protocol);
    }
}

message Message2V6
{
    IPv6Address SourceAddress with Visualization{AliasName = "Address"};
    IPv6Address DestinationAddress with Visualization{AliasName = "Address"};
    byte Protocol;
    long FlowContext;
    short PayloadLength;
    binary Payload;
    
    override string ToString()
    {
        return "Message2V6, Protocol: " + EnumToStringInSummary<IANA.ProtocolType>(Protocol);
    }
}

message AuthMessageV4
{
    IPv4Address   SourceAddress with Visualization{AliasName = "Address"};
    IPv4Address   DestinationAddress with Visualization{AliasName = "Address"};
    ushort  SourcePort;
    ushort  DestinationPort;
    long   InterfaceId;
    byte   Direction;
    byte   Protocol;
    long   FlowContext;
    long   ProcessId;
    short  PathLength;
    string ProcessPath with BinaryEncoding{TextEncoding = TextEncoding.UTF16};

    override string ToString()
    {
        return "WFP ALE-AUTH V4, pid: " + (ProcessId as string) + ", app path: " + ProcessPath;
    }
}

message AuthMessageV6
{
    IPv6Address SourceAddress with Visualization{AliasName = "Address"};
    IPv6Address DestinationAddress with Visualization{AliasName = "Address"};
    ushort  SourcePort;
    ushort  DestinationPort;
    long   InterfaceId;
    byte   Direction;
    byte   Protocol;
    long   FlowContext;
    long   ProcessId;
    short  PathLength;
    string ProcessPath with BinaryEncoding{TextEncoding = TextEncoding.UTF16};

    override string ToString()
    {
        return "WFP ALE-AUTH V6, pid: " + (ProcessId as string) + ", app path: " + ProcessPath;
    }
}

message Discard
{
    FWPS_DISCARD_MODULE DiscardModule;
    (
      [| DiscardModule == FWPS_DISCARD_MODULE.FWPS_DISCARD_MODULE_NETWORK |] IP_DISCARD_REASON
    | [| DiscardModule == FWPS_DISCARD_MODULE.FWPS_DISCARD_MODULE_TRANSPORT |] INET_DISCARD_REASON
    | [| DiscardModule == FWPS_DISCARD_MODULE.FWPS_DISCARD_MODULE_GENERAL |] FWPS_GENERAL_DISCARD_REASON
    )
    DiscardReason;
    
    ulong FilterId;
    
    override string ToString()
    {
        return "Discard, DiscardModule: " + DiscardModule.ToString() + ", DiscardReason: " + DiscardReason.ToString() + ", FilterId: " + (FilterId as string);
    }
}

IPv4Address get Source(this WFPCapture.MessageV4 msg)
{
    return msg.SourceAddress;
}

IPv4Address get Destination(this WFPCapture.MessageV4 msg)
{
    return msg.DestinationAddress;
}

IPv6Address get Source(this WFPCapture.MessageV6 msg)
{
    return msg.SourceAddress;
}

IPv6Address get Destination(this WFPCapture.MessageV6 msg)
{
    return msg.DestinationAddress;
}

string get Network(this WFPCapture.MessageV4 m)
{
    string src = GetAlias("Utility.IPv4Address", m.SourceAddress, m) ?? m.SourceAddress.ToString();
    string dst = GetAlias("Utility.IPv4Address", m.DestinationAddress, m) ?? m.DestinationAddress.ToString();
    return (m.SourceAddress.Octets > m.DestinationAddress.Octets ? (dst + " - " + src) : (src + " - " + dst));
}

string get Network(this WFPCapture.MessageV6 m)
{
    string src = GetAlias("Utility.IPv6Address", m.SourceAddress, m) ?? m.SourceAddress.ToString();
    string dst = GetAlias("Utility.IPv6Address", m.DestinationAddress, m) ?? m.DestinationAddress.ToString();
    return (m.SourceAddress.Octets > m.DestinationAddress.Octets ? (dst + " - " + src) : (src + " - " + dst));
}

IPv4Address get Source(this WFPCapture.Message2V4 msg)
{
    return msg.SourceAddress;
}

IPv4Address get Destination(this WFPCapture.Message2V4 msg)
{
    return msg.DestinationAddress;
}

IPv6Address get Source(this WFPCapture.Message2V6 msg)
{
    return msg.SourceAddress;
}

IPv6Address get Destination(this WFPCapture.Message2V6 msg)
{
    return msg.DestinationAddress;
}

string get Network(this WFPCapture.Message2V4 m)
{
    string src = GetAlias("Utility.IPv4Address", m.SourceAddress, m) ?? m.SourceAddress.ToString();
    string dst = GetAlias("Utility.IPv4Address", m.DestinationAddress, m) ?? m.DestinationAddress.ToString();
    return (m.SourceAddress.Octets > m.DestinationAddress.Octets ? (dst + " - " + src) : (src + " - " + dst));
}

string get Network(this WFPCapture.Message2V6 m)
{
    string src = GetAlias("Utility.IPv6Address", m.SourceAddress, m) ?? m.SourceAddress.ToString();
    string dst = GetAlias("Utility.IPv6Address", m.DestinationAddress, m) ?? m.DestinationAddress.ToString();
    return (m.SourceAddress.Octets > m.DestinationAddress.Octets ? (dst + " - " + src) : (src + " - " + dst));
}

IPv4Address get Source(this WFPCapture.AuthMessageV4 msg)
{
    return msg.SourceAddress;
}

IPv4Address get Destination(this WFPCapture.AuthMessageV4 msg)
{
    return msg.DestinationAddress;
}

IPv6Address get Source(this WFPCapture.AuthMessageV6 msg)
{
    return msg.SourceAddress;
}

IPv6Address get Destination(this WFPCapture.AuthMessageV6 msg)
{
    return msg.DestinationAddress;
}

string get Network(this WFPCapture.AuthMessageV4 m)
{
    string src = GetAlias("Utility.IPv4Address", m.SourceAddress, m) ?? m.SourceAddress.ToString();
    string dst = GetAlias("Utility.IPv4Address", m.DestinationAddress, m) ?? m.DestinationAddress.ToString();
    return (m.SourceAddress.Octets > m.DestinationAddress.Octets ? (dst + " - " + src) : (src + " - " + dst));
}

string get Network(this WFPCapture.AuthMessageV6 m)
{
    string src = GetAlias("Utility.IPv6Address", m.SourceAddress, m) ?? m.SourceAddress.ToString();
    string dst = GetAlias("Utility.IPv6Address", m.DestinationAddress, m) ?? m.DestinationAddress.ToString();
    return (m.SourceAddress.Octets > m.DestinationAddress.Octets ? (dst + " - " + src) : (src + " - " + dst));
}

// Diagnostic Driver Operation Events
message DriverError
{
    string ErrorMessage;
    uint NTStatus with DisplayInfo{ToText = NTStatusToText};
    
    override string ToString()
    {
        return "DriverError, NTStatus: " + ERREF.NtStatusValuesToText(NTStatus) + ", ErrorMessage: " + ErrorMessage;
    }
    
    static string NTStatusToText(any status)
    {
        return ERREF.NtStatusValuesToText(status as uint);
    }
}

message CalloutError
{
    CalloutMap Callout;
    string ErrorMessage;
    uint NTStatus with DisplayInfo{ToText = NTStatusToText};
    
    override string ToString()
    {
        return "CalloutError, Callout: " + EnumToString<CalloutMap>(Callout) + ", NTStatus: " + ERREF.NtStatusValuesToText(NTStatus) + ", ErrorMessage: " + ErrorMessage;
    }
    
    static string NTStatusToText(any status)
    {
        return ERREF.NtStatusValuesToText(status as uint);
    }
}

message DriverInfo
{
    string DriverName;
    ushort MajorVersion;
    ushort MinorVersion;
    
    override string ToString()
    {
        return "DriverInfo, DriverName: " + DriverName + ", MajorVersion: " + (MajorVersion as string) + ", MinorVersion: " + (MinorVersion as string);
    }
}

message CalloutInfo
{
    CalloutMap Callout;
    
    override string ToString()
    {
        return "CalloutInfo, Callout: " + EnumToStringInSummary<CalloutMap>(Callout);
    }
}

message CalloutNotifyInfo
{
    ulong FilterId;
    CalloutMap Callout;
    ulong FilterWeight;

    override string ToString()
    {
        return "CalloutNotifyInfo, Callout: " + EnumToStringInSummary<CalloutMap>(Callout) + ", FilterId: " + (FilterId as string) + ", FilterWeight: " + (FilterWeight as string);
    }
}

pattern CalloutMap = enum uint
{
    CALLOUT_INBOUND_TRANSPORT_V4 = 0,
    CALLOUT_OUTBOUND_TRANSPORT_V4,
    CALLOUT_INBOUND_TRANSPORT_V6,
    CALLOUT_OUTBOUND_TRANSPORT_V6,
    CALLOUT_ALE_AUTH_CONNECT_V4,
    CALLOUT_ALE_AUTH_CONNECT_V6,
    CALLOUT_ALE_AUTH_RECV_ACCEPT_V4,
    CALLOUT_ALE_AUTH_RECV_ACCEPT_V6,
    CALLOUT_INBOUND_IPPACKET_V4_DISCARD,
    CALLOUT_INBOUND_IPPACKET_V6_DISCARD,
    CALLOUT_OUTBOUND_IPPACKET_V4_DISCARD,
    CALLOUT_OUTBOUND_IPPACKET_V6_DISCARD,
    CALLOUT_IPFORWARD_V4_DISCARD,
    CALLOUT_IPFORWARD_V6_DISCARD,
    CALLOUT_INBOUND_TRANSPORT_V4_DISCARD,
    CALLOUT_INBOUND_TRANSPORT_V6_DISCARD,
    CALLOUT_OUTBOUND_TRANSPORT_V4_DISCARD,
    CALLOUT_OUTBOUND_TRANSPORT_V6_DISCARD,
    // Future Reference: stream discard layers not implemented.
    CALLOUT_DATAGRAM_DATA_V4_DISCARD,
    CALLOUT_DATAGRAM_DATA_V6_DISCARD,
    CALLOUT_INBOUND_ICMP_ERROR_V4_DISCARD,
    CALLOUT_INBOUND_ICMP_ERROR_V6_DISCARD,
    CALLOUT_OUTBOUND_ICMP_ERROR_V4_DISCARD,
    CALLOUT_OUTBOUND_ICMP_ERROR_V6_DISCARD,
    CALLOUT_ALE_RESOURCE_ASSIGNMENT_V4_DISCARD,
    CALLOUT_ALE_RESOURCE_ASSIGNMENT_V6_DISCARD,
    CALLOUT_ALE_AUTH_LISTEN_V4_DISCARD,
    CALLOUT_ALE_AUTH_LISTEN_V6_DISCARD,
    CALLOUT_ALE_AUTH_RECV_ACCEPT_V4_DISCARD,
    CALLOUT_ALE_AUTH_RECV_ACCEPT_V6_DISCARD,
    CALLOUT_ALE_AUTH_CONNECT_V4_DISCARD,
    CALLOUT_ALE_AUTH_CONNECT_V6_DISCARD,
    CALLOUT_ALE_FLOW_ESTABLISHED_V4_DISCARD,
    CALLOUT_ALE_FLOW_ESTABLISHED_V6_DISCARD
};
