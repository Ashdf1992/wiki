protocol MSOXWUMS with
Documentation
{
    ProtocolName = "Voice Mail Settings Web Service Protocol",
    ShortName = "MS-OXWUMS",
    DocumentName = "MS-OXWUMS",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2015 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-OXWUMS", Version = "10.0"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="14/09/2015"},
        ]
};

using SOAP;

const map<string, string> SoapActionsMap =
{
    "http://schemas.microsoft.com/exchange/services/2006/messages/IsUMEnabled" -> "IsUMEnabled",
    "http://schemas.microsoft.com/exchange/services/2006/messages/GetUMProperties" -> "GetUMProperties",
    "http://schemas.microsoft.com/exchange/services/2006/messages/SetOofStatus" -> "SetOofStatus",
    "http://schemas.microsoft.com/exchange/services/2006/messages/SetPlayOnPhoneDialString" -> "SetPlayOnPhoneDialString",
    "http://schemas.microsoft.com/exchange/services/2006/messages/SetTelephoneAccessFolderEmail" -> "SetTelephoneAccessFolderEmail",
    "http://schemas.microsoft.com/exchange/services/2006/messages/SetMissedCallNotificationEnabled" -> "SetMissedCallNotificationEnabled",
    "http://schemas.microsoft.com/exchange/services/2006/messages/ResetPIN" -> "ResetPIN",
    "http://schemas.microsoft.com/exchange/services/2006/messages/PlayOnPhone" -> "PlayOnPhone",
    "http://schemas.microsoft.com/exchange/services/2006/messages/GetCallInfo" -> "GetCallInfo",
    "http://schemas.microsoft.com/exchange/services/2006/messages/Disconnect" -> "Disconnect",
    "http://schemas.microsoft.com/exchange/services/2006/messages/PlayOnPhoneGreeting" -> "PlayOnPhoneGreeting",
    "http://schemas.microsoft.com/exchange/services/2006/messages/GetPhoneCallInformation" -> "GetPhoneCallInformation",
    "http://schemas.microsoft.com/exchange/services/2006/messages/DisconnectPhoneCall" -> "DisconnectPhoneCall",
	"http://schemas.microsoft.com/exchange/services/2006/messages/PlayOnPhone2" -> "PlayOnPhone2"	 
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer)
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        RequestMessage msg = new RequestMessage();
        msg.Initialize(soapMsg);
        dispatch (endpoint Server over soapServer) accepts msg;
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        ResponseMessage msg = new ResponseMessage();
        msg.Initialize(soapMsg);
        dispatch (endpoint Server over soapServer) issues msg;
    }
}

message MessageBase
{
    string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        this.SoapAction = soapMsg.SoapAction as string;
        this.ContainsFault = soapMsg.ContainsFault;
        
        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        return SoapActionsMap[this.SoapAction];
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}