protocol MSWEBSS with
Documentation
{
    ProtocolName = "Webs Web Service Protocol",
    ShortName = "MS-WEBSS",
    DocumentName = "MS-WEBSS",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-WEBSS", Version = "3.3"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSWEBSS";

const map<string, string> SoapActionsMap =
{
    "http://schemas.microsoft.com/sharepoint/soap/CreateContentType" -> "CreateContentType",
    "http://schemas.microsoft.com/sharepoint/soap/CustomizeCss" -> "CustomizeCss",
    "http://schemas.microsoft.com/sharepoint/soap/DeleteContentType" -> "DeleteContentType",
    "http://schemas.microsoft.com/sharepoint/soap/GetActivatedFeatures" -> "GetActivatedFeatures",
    "http://schemas.microsoft.com/sharepoint/soap/GetAllSubWebCollection" -> "GetAllSubWebCollection",
    "http://schemas.microsoft.com/sharepoint/soap/GetColumns" -> "GetColumns",
    "http://schemas.microsoft.com/sharepoint/soap/GetContentType" -> "GetContentType",
    "http://schemas.microsoft.com/sharepoint/soap/GetContentTypes" -> "GetContentTypes",
    "http://schemas.microsoft.com/sharepoint/soap/GetCustomizedPageStatus" -> "GetCustomizedPageStatus",
    "http://schemas.microsoft.com/sharepoint/soap/GetListTemplates" -> "GetListTemplates",
    "http://schemas.microsoft.com/sharepoint/soap/GetObjectIdFromUrl" -> "GetObjectIdFromUrl",
    "http://schemas.microsoft.com/sharepoint/soap/GetWeb" -> "GetWeb",
    "http://schemas.microsoft.com/sharepoint/soap/GetWebCollection" -> "GetWebCollection",
    "http://schemas.microsoft.com/sharepoint/soap/RemoveContentTypeXmlDocument" -> "RemoveContentTypeXmlDocument",
    "http://schemas.microsoft.com/sharepoint/soap/RevertAllFileContentStreams" -> "RevertAllFileContentStreams",
    "http://schemas.microsoft.com/sharepoint/soap/RevertCss" -> "RevertCss",
    "http://schemas.microsoft.com/sharepoint/soap/RevertFileContentStream" -> "RevertFileContentStream",
    "http://schemas.microsoft.com/sharepoint/soap/UpdateColumns" -> "UpdateColumns",
    "http://schemas.microsoft.com/sharepoint/soap/UpdateContentType" -> "UpdateContentType",
    "http://schemas.microsoft.com/sharepoint/soap/UpdateContentTypeXmlDocument" -> "UpdateContentTypeXmlDocument",
    "http://schemas.microsoft.com/sharepoint/soap/WebUrlFromPageUrl" -> "WebUrlFromPageUrl"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        this.SoapAction = soapMsg.SoapAction as string;
        this.ContainsFault = soapMsg.ContainsFault;
        
        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        return SoapActionsMap[this.SoapAction];
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}
