protocol MSAADT with
Documentation
{
    ProtocolName = "Access Application Design Time Protocol",
    ShortName = "MS-AADT",
    DocumentName = "MS-AADT",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-AADT", Version = "1.2"}, 
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSAADT";

const map<string, string> SoapActionsMap =
{
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/CreateAccessObject" -> "CreateAccessObject",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/CreatePackage" -> "CreatePackage",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/GetAccessObject" -> "GetAccessObject",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/GetApplicationInformation" -> "GetApplicationInformation",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/GetLinkedTableInformation" -> "GetLinkedTableInformation",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/RenameAccessObject" -> "RenameAccessObject",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/SetDataMacroTracing" -> "SetDataMacroTracing",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/UpdateAccessObject" -> "UpdateAccessObject",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/DeleteAccessObject" -> "DeleteAccessObject",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/AddFirewallRule" -> "AddFirewallRule",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/DeleteFirewallRules" -> "DeleteFirewallRules",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/GetDataConnectivity" -> "GetDataConnectivity",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/GetDataConnectivityStatus" -> "GetDataConnectivityStatus",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/GetFirewallRules" -> "GetFirewallRules",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/ImportData" -> "ImportData",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/RefreshLinkedTableInformation" -> "RefreshLinkedTableInformation",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/SetDataConnectivityStatus" -> "SetDataConnectivityStatus",
    "http://schemas.microsoft.com/office/Access/2010/11/Server/WebServices/ApplicationDesignService/GetPackage" -> "GetPackage"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        this.SoapAction = soapMsg.SoapAction as string;
        this.ContainsFault = soapMsg.ContainsFault;
        
        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        return SoapActionsMap[this.SoapAction];
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}
