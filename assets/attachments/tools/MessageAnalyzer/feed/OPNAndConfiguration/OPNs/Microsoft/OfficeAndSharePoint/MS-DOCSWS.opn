protocol MSDOCSWS with
Documentation
{
    ProtocolName = "SharePoint Document Sharing Web Service Protocol",
    ShortName = "MS-DOCSWS",
    DocumentName = "MS-DOCSWS",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-DOCSWS", Version = "1.1"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSDOCSWS";

const map<string, string> WsaActionsMap =
{
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetHostSharingCapabilities" -> "GetHostSharingCapabilities",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetHostSharingCapabilitiesResponse" -> "GetHostSharingCapabilities",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetHostSharingCapabilitiesSharingServerErrorFault" -> "GetHostSharingCapabilities",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetUserSharingAttributes" -> "GetUserSharingAttributes",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetUserSharingAttributesResponse" -> "GetUserSharingAttributes",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetUserSharingAttributesSharingServerErrorFault" -> "GetUserSharingAttributes",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetPermissions" -> "GetPermissions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetPermissionsResponse" -> "GetPermissions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetPermissionsSharingServerErrorFault" -> "GetPermissions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/SetPermissions" -> "SetPermissions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/SetPermissionsResponse" -> "SetPermissions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/SetPermissionsSharingServerErrorFault" -> "SetPermissions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetLinks" -> "GetLinks",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetLinksResponse" -> "GetLinks",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetLinksSharingServerErrorFault" -> "GetLinks",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/SetLinks" -> "SetLinks",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/SetLinksResponse" -> "SetLinks",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/SetLinksSharingServerErrorFault" -> "SetLinks",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetVersions" -> "GetVersions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetVersionsResponse" -> "GetVersions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetVersionsSharingServerErrorFault" -> "GetVersions"
};

const map<string, string> SoapActionsMap = {
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetHostSharingCapabilities" -> "GetHostSharingCapabilities",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetUserSharingAttributes" -> "GetUserSharingAttributes",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetPermissions" -> "GetPermissions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/SetPermissions" -> "SetPermissions",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetLinks" -> "GetLinks",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/SetLinks" -> "SetLinks",
    "http://schemas.microsoft.com/clouddocuments/DocumentSharing/GetVersions" -> "GetVersions"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    optional string WsaAction;
    optional string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        if (soapMsg.WsaAction != "")
            this.WsaAction = soapMsg.WsaAction;
            
        if (soapMsg.SoapAction != "")
            this.SoapAction = soapMsg.SoapAction;
            
        this.ContainsFault = soapMsg.ContainsFault;

        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        if (this.WsaAction is wsaAcction:string)
            return WsaActionsMap[wsaAcction];
            
        if (this.SoapAction is soapAcction:string)
            return SoapActionsMap[soapAcction];
            
        return "Unknown";
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}