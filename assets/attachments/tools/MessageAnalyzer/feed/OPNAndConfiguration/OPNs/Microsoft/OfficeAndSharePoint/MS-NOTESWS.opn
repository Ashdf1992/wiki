protocol MSNOTESWS with
Documentation
{
    ProtocolName = "MS Search Lotus Notes Web Service Protocol",
    ShortName = "MS-NOTESWS",
    DocumentName = "MS-NOTESWS",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-NOTESWS", Version = "3.1"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSNOTESWS";

const map<string, string> WsaActionsMap =
{
    "http://tempuri.org/INotesWebServiceApplication/EnumerateServers" -> "EnumerateServers",
    "http://tempuri.org/INotesWebServiceApplication/EnumerateServersResponse" -> "EnumerateServers",
    "http://tempuri.org/INotesWebServiceApplication/GetServer" -> "GetServer",
    "http://tempuri.org/INotesWebServiceApplication/GetServerResponse" -> "GetServer",
    "http://tempuri.org/INotesWebServiceApplication/EnumerateDatabases" -> "EnumerateDatabases",
    "http://tempuri.org/INotesWebServiceApplication/EnumerateDatabasesResponse" -> "EnumerateDatabases",
    "http://tempuri.org/INotesWebServiceApplication/GetDatabase" -> "GetDatabase",
    "http://tempuri.org/INotesWebServiceApplication/GetDatabaseResponse" -> "GetDatabase",
    "http://tempuri.org/INotesWebServiceApplication/EnumerateItems" -> "EnumerateItems",
    "http://tempuri.org/INotesWebServiceApplication/EnumerateItemsResponse" -> "EnumerateItems",
    "http://tempuri.org/INotesWebServiceApplication/FetchItem" -> "FetchItem",
    "http://tempuri.org/INotesWebServiceApplication/FetchItemResponse" -> "FetchItem",
    "http://tempuri.org/INotesWebServiceApplication/FetchAttachment" -> "FetchAttachment",
    "http://tempuri.org/INotesWebServiceApplication/FetchAttachmentResponse" -> "FetchAttachment"
};

const map<string, string> SoapActionsMap = {
    "http://tempuri.org/INotesWebServiceApplication/EnumerateServers" -> "EnumerateServers",
    "http://tempuri.org/INotesWebServiceApplication/GetServer" -> "GetServer",
    "http://tempuri.org/INotesWebServiceApplication/EnumerateDatabases" -> "EnumerateDatabases",
    "http://tempuri.org/INotesWebServiceApplication/GetDatabase" -> "GetDatabase",
    "http://tempuri.org/INotesWebServiceApplication/EnumerateItems" -> "EnumerateItems",
    "http://tempuri.org/INotesWebServiceApplication/FetchItem" -> "FetchItem",
    "http://tempuri.org/INotesWebServiceApplication/FetchAttachment" -> "FetchAttachment"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    optional string WsaAction;
    optional string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        if (soapMsg.WsaAction != "")
            this.WsaAction = soapMsg.WsaAction;
            
        if (soapMsg.SoapAction != "")
            this.SoapAction = soapMsg.SoapAction;
            
        this.ContainsFault = soapMsg.ContainsFault;

        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        if (this.WsaAction is wsaAcction:string)
            return WsaActionsMap[wsaAcction];
            
        if (this.SoapAction is soapAcction:string)
            return SoapActionsMap[soapAcction];
            
        return "Unknown";
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}