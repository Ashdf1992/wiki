protocol MSWMS with
Documentation
{
    ProtocolName = "Work Management Service Web Service Protocol",
    ShortName = "MS-WMS",
    DocumentName = "MS-WMS",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-WMS", Version = "2.0"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSWMS";

const map<string, string> WsaActionsMap =
{
    "http://tempuri.org/IWorkManagementServiceApplication/BeginCacheRefresh" -> "BeginCacheRefresh",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginCacheRefreshResponse" -> "BeginCacheRefresh",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginExchangeSync" -> "BeginExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginExchangeSyncResponse" -> "BeginExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/OptIntoExchangeSync" -> "OptIntoExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/OptIntoExchangeSyncResponse" -> "OptIntoExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/OptOutOfExchangeSync" -> "OptOutOfExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/OptOutOfExchangeSyncResponse" -> "OptOutOfExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/GetExchangeSyncInfo" -> "GetExchangeSyncInfo",
    "http://tempuri.org/IWorkManagementServiceApplication/GetExchangeSyncInfoResponse" -> "GetExchangeSyncInfo",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginCreateTaskPropagation" -> "BeginCreateTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginCreateTaskPropagationResponse" -> "BeginCreateTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginUpdateTaskPropagation" -> "BeginUpdateTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginUpdateTaskPropagationResponse" -> "BeginUpdateTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginDeleteTaskPropagation" -> "BeginDeleteTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginDeleteTaskPropagationResponse" -> "BeginDeleteTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginRelocateTaskPropagation" -> "BeginRelocateTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginRelocateTaskPropagationResponse" -> "BeginRelocateTaskPropagation"
};

const map<string, string> SoapActionsMap = {
    "http://tempuri.org/IWorkManagementServiceApplication/BeginCacheRefresh" -> "BeginCacheRefresh",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginExchangeSync" -> "BeginExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/OptIntoExchangeSync" -> "OptIntoExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/OptOutOfExchangeSync" -> "OptOutOfExchangeSync",
    "http://tempuri.org/IWorkManagementServiceApplication/GetExchangeSyncInfo" -> "GetExchangeSyncInfo",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginCreateTaskPropagation" -> "BeginCreateTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginUpdateTaskPropagation" -> "BeginUpdateTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginDeleteTaskPropagation" -> "BeginDeleteTaskPropagation",
    "http://tempuri.org/IWorkManagementServiceApplication/BeginRelocateTaskPropagation" -> "BeginRelocateTaskPropagation"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    optional string WsaAction;
    optional string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        if (soapMsg.WsaAction != "")
            this.WsaAction = soapMsg.WsaAction;
            
        if (soapMsg.SoapAction != "")
            this.SoapAction = soapMsg.SoapAction;
            
        this.ContainsFault = soapMsg.ContainsFault;

        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        if (this.WsaAction is wsaAcction:string)
            return WsaActionsMap[wsaAcction];
            
        if (this.SoapAction is soapAcction:string)
            return SoapActionsMap[soapAcction];
            
        return "Unknown";
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}