protocol MSWORDSWCF with
Documentation
{
    ProtocolName = "Word Automation Services WCF Service Protocol",
    ShortName = "MS-WORDSWCF",
    DocumentName = "MS-WORDSWCF",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-WORDSWCF", Version = "4.0"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSWORDSWCF";

const map<string, string> WsaActionsMap =
{
    "http://schemas.microsoft.com/office/server/word/2009/08/ping" -> "Ping",
    "http://schemas.microsoft.com/office/server/word/2009/08/ping/response" -> "Ping",
    "http://schemas.microsoft.com/office/server/word/2009/08/cancelJob" -> "CancelJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/cancelJob/response" -> "CancelJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/readOnlyFault" -> "CancelJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/getJobStatus" -> "GetJobStatus",
    "http://schemas.microsoft.com/office/server/word/2009/08/getJobStatus/response" -> "GetJobStatus",
    "http://schemas.microsoft.com/office/server/word/2009/08/getSyncJobStatus" -> "BatchGetSyncJobStatus",
    "http://schemas.microsoft.com/office/server/word/2009/08/getSyncJobStatus/response" -> "BatchGetSyncJobStatus",
    "http://schemas.microsoft.com/office/server/word/2009/08/addSyncJob" -> "AddSyncJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/addSyncJob/response" -> "AddSyncJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/addSyncStreamJob" -> "AddSyncStreamJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/addSyncStreamJob/response" -> "AddSyncStreamJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/getSyncStreamOutputBytes" -> "GetSyncStreamOutputBytes",
    "http://schemas.microsoft.com/office/server/word/2009/08/getSyncStreamOutputBytes/response" -> "GetSyncStreamOutputBytes",
    "http://schemas.microsoft.com/office/server/word/2009/08/addJob" -> "AddJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/addJob/response" -> "AddJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/addGroup" -> "AddGroup",
    "http://schemas.microsoft.com/office/server/word/2009/08/addGroup/response" -> "AddGroup",
    "http://schemas.microsoft.com/office/server/word/2009/08/addItems" -> "AddItems",
    "http://schemas.microsoft.com/office/server/word/2009/08/submitJob" -> "SubmitJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/submitJob/response" -> "SubmitJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/getJobs" -> "GetJobs",
    "http://schemas.microsoft.com/office/server/word/2009/08/getJobs/response" -> "GetJobs",
    "http://schemas.microsoft.com/office/server/word/2009/08/getGroups" -> "GetGroups",
    "http://schemas.microsoft.com/office/server/word/2009/08/getGroups/response" -> "GetGroups",
    "http://schemas.microsoft.com/office/server/word/2009/08/getItems" -> "GetItems",
    "http://schemas.microsoft.com/office/server/word/2009/08/getItems/response" -> "GetItems",
    "http://schemas.microsoft.com/office/server/word/2009/08/convertBatch" -> "ConvertBatch",
    "http://schemas.microsoft.com/office/server/word/2009/08/getId" -> "GetId",
    "http://schemas.microsoft.com/office/server/word/2009/08/getId/response" -> "GetId"
};

const map<string, string> SoapActionsMap = {
    "http://schemas.microsoft.com/office/server/word/2009/08/ping" -> "Ping",
    "http://schemas.microsoft.com/office/server/word/2009/08/cancelJob" -> "CancelJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/getJobStatus" -> "GetJobStatus",
    "http://schemas.microsoft.com/office/server/word/2009/08/getSyncJobStatus" -> "BatchGetSyncJobStatus",
    "http://schemas.microsoft.com/office/server/word/2009/08/addSyncJob" -> "AddSyncJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/addSyncStreamJob" -> "AddSyncStreamJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/getSyncStreamOutputBytes" -> "GetSyncStreamOutputBytes",
    "http://schemas.microsoft.com/office/server/word/2009/08/addJob" -> "AddJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/addGroup" -> "AddGroup",
    "http://schemas.microsoft.com/office/server/word/2009/08/addItems" -> "AddItems",
    "http://schemas.microsoft.com/office/server/word/2009/08/submitJob" -> "SubmitJob",
    "http://schemas.microsoft.com/office/server/word/2009/08/getJobs" -> "GetJobs",
    "http://schemas.microsoft.com/office/server/word/2009/08/getGroups" -> "GetGroups",
    "http://schemas.microsoft.com/office/server/word/2009/08/getItems" -> "GetItems",
    "http://schemas.microsoft.com/office/server/word/2009/08/convertBatch" -> "ConvertBatch",
    "http://schemas.microsoft.com/office/server/word/2009/08/getId" -> "GetId"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    optional string WsaAction;
    optional string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        if (soapMsg.WsaAction != "")
            this.WsaAction = soapMsg.WsaAction;
            
        if (soapMsg.SoapAction != "")
            this.SoapAction = soapMsg.SoapAction;
            
        this.ContainsFault = soapMsg.ContainsFault;

        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        if (this.WsaAction is wsaAcction:string)
            return WsaActionsMap[wsaAcction];
            
        if (this.SoapAction is soapAcction:string)
            return SoapActionsMap[soapAcction];
            
        return "Unknown";
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}