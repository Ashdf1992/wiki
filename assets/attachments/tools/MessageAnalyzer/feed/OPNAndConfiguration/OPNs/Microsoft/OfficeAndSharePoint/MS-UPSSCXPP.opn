protocol MSUPSSCXPP with
Documentation
{
    ProtocolName = "User Profile Synchronization (UPS): Schema Exchange Protocol Profile",
    ShortName = "MS-UPSSCXPP",
    DocumentName = "MS-UPSSCXPP",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-UPSSCXPP", Version = "1.1"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;
using WSTransfer;

const string ModuleName = "MSUPSSCXPP";

const map<string, string> WsaActionsMap =
{
    "http://schemas.xmlsoap.org/ws/2004/09/transfer/Get" -> "Get",
    "http://schemas.xmlsoap.org/ws/2004/09/transfer/GetResponse" -> "Get"
};

const map<string, string> SoapActionsMap = {
    "http://schemas.xmlsoap.org/ws/2004/09/transfer/Get" -> "Get"
};

pattern MSUPSSCXPP_EndpointUrl = regex {.*/_[vV][tT][iI]_[bB][iI][nN]/[oO][fF][fF][iI][cC][iI][aA][lL][fF][iI][lL][eE][.][aA][sS][mM][xX]};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP precedes WSTransfer.ResourceMessageActor
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            string httpUri = GetHttpUri(soapMsg);
            if (!(httpUri is MSUPSSCXPP_EndpointUrl))
            {
                reject;
            }
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            string httpUri = GetHttpUri(soapMsg);
            if (!(httpUri is MSUPSSCXPP_EndpointUrl))
            {
                reject;
            }
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    optional string WsaAction;
    optional string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        if (soapMsg.WsaAction != "")
            this.WsaAction = soapMsg.WsaAction;
            
        if (soapMsg.SoapAction != "")
            this.SoapAction = soapMsg.SoapAction;
            
        this.ContainsFault = soapMsg.ContainsFault;

        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        if (this.WsaAction is wsaAcction:string)
            return WsaActionsMap[wsaAcction];
            
        if (this.SoapAction is soapAcction:string)
            return SoapActionsMap[soapAcction];
            
        return "Unknown";
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}