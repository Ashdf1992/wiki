protocol MSCDEPLOY with
Documentation
{
    ProtocolName = "Content Deployment Remote Import Web Service Protocol",
    ShortName = "MS-CDEPLOY",
    DocumentName = "MS-CDEPLOY",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-CDEPLOY", Version = "3.2"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSCDEPLOY";

const map<string, string> SoapActionsMap =
{
    "http://schemas.microsoft.com/sharepoint/soap/CancelJob" -> "CancelJob",
    "http://schemas.microsoft.com/sharepoint/soap/CreateJob" -> "CreateJob",
    "http://schemas.microsoft.com/sharepoint/soap/DeleteImportLogs" -> "DeleteImportLogs",
    "http://schemas.microsoft.com/sharepoint/soap/DeleteJob" -> "DeleteJob",
    "http://schemas.microsoft.com/sharepoint/soap/GetJobStatus" -> "GetJobStatus",
    "http://schemas.microsoft.com/sharepoint/soap/GetRemoteAdminServerUrl" -> "GetRemoteAdminServerUrl",
    "http://schemas.microsoft.com/sharepoint/soap/GetSiteCollectionNames" -> "GetSiteCollectionNames",
    "http://schemas.microsoft.com/sharepoint/soap/GetVirtualServersInformation" -> "GetVirtualServersInformation",
    "http://schemas.microsoft.com/sharepoint/soap/RunJob" -> "RunJob"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        this.SoapAction = soapMsg.SoapAction as string;
        this.ContainsFault = soapMsg.ContainsFault;
        
        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        return SoapActionsMap[this.SoapAction];
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}

