protocol MSOXWSADISC with
Documentation
{
    ProtocolName = "Autodiscover Publishing and Lookup SOAP-Based Web Service Protocol",
    ShortName = "MS-OXWSADISC",
    DocumentName = "MS-OXWSADISC",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2016 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-OXWSADISC", Version = "15.0"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="14/09/2015"},
        ]
};

using SOAP;

const map<string, string> WsaActionsMap =
{
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetUserSettings" -> "GetUserSettings",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetUserSettingsResponse" -> "GetUserSettings",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetDomainSettings" -> "GetDomainSettings",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetDomainSettingsResponse" -> "GetDomainSettings",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetFederationInformation" -> "GetFederationInformation",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetFederationInformationResponse" -> "GetFederationInformation",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetOrganizationRelationshipSettings" -> "GetOrganizationRelationshipSettings",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetOrganizationRelationshipSettingsResponse" -> "GetOrganizationRelationshipSettings"
};

const map<string, string> SoapActionsMap = {
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetUserSettings" -> "GetUserSettings",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetDomainSettings" -> "GetDomainSettings",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetFederationInformation" -> "GetFederationInformation",
    "http://schemas.microsoft.com/exchange/2010/Autodiscover/Autodiscover/GetOrganizationRelationshipSettings" -> "GetOrganizationRelationshipSettings"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer)
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        RequestMessage msg = new RequestMessage();
        msg.Initialize(soapMsg);
        dispatch (endpoint Server over soapServer) accepts msg;
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        ResponseMessage msg = new ResponseMessage();
        msg.Initialize(soapMsg);
        dispatch (endpoint Server over soapServer) issues msg;
    }
}

message MessageBase
{
    optional string WsaAction;
    optional string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        if (soapMsg.WsaAction != "")
            this.WsaAction = soapMsg.WsaAction;
            
        if (soapMsg.SoapAction != "")
            this.SoapAction = soapMsg.SoapAction;
            
        this.ContainsFault = soapMsg.ContainsFault;

        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        if (this.WsaAction is wsaAcction:string)
            return WsaActionsMap[wsaAcction];
            
        if (this.SoapAction is soapAcction:string)
            return SoapActionsMap[soapAcction];
            
        return "Unknown";
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}