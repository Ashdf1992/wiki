protocol MSUPIEWS with
Documentation
{
    ProtocolName = "User Profile Import and Export Web Service Protocol",
    ShortName = "MS-UPIEWS",
    DocumentName = "MS-UPIEWS",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-UPIEWS", Version = "2.2"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSUPIEWS";

const map<string, string> SoapActionsMap =
{
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/FinalizeProfileImportExportProcess" -> "FinalizeProfileImportExportProcess",
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/GetImportProperties" -> "GetImportProperties",
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/GetPartitionIds" -> "GetPartitionIds",
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/InitializeProfileImportExportProcess" -> "InitializeProfileImportExportProcess",
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/RetrieveBDCProfileChangeData" -> "RetrieveBDCProfileChangeData",
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/RetrieveProfileChangeDataDelta" -> "RetrieveProfileChangeDataDelta",
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/RetrieveProfileChangeDataFull" -> "RetrieveProfileChangeDataFull",
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/UpdateWithProfileChangeData" -> "UpdateWithProfileChangeData",
    "http://microsoft.com/webservices/SharePointPortalServer/ProfileImportExportService/GetProfileImportClientMode" -> "GetProfileImportClientMode"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        this.SoapAction = soapMsg.SoapAction as string;
        this.ContainsFault = soapMsg.ContainsFault;
        
        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        return SoapActionsMap[this.SoapAction];
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}

