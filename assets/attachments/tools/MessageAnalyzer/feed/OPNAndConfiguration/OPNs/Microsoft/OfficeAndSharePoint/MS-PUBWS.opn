protocol MSPUBWS with
Documentation
{
    ProtocolName = "Publishing Web Service Protocol",
    ShortName = "MS-PUBWS",
    DocumentName = "MS-PUBWS",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-PUBWS", Version = "3.2"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSPUBWS";

const map<string, string> SoapActionsMap =
{
    "http://schemas.microsoft.com/sharepoint/soap/CreatePageLayout" -> "CreatePageLayout",
    "http://schemas.microsoft.com/sharepoint/soap/DisconnectPageLayout" -> "DisconnectPageLayout",
    "http://schemas.microsoft.com/sharepoint/soap/ReconnectPageLayout" -> "ReconnectPageLayout",
    "http://schemas.microsoft.com/sharepoint/soap/ExportObjects" -> "ExportObjects",
    "http://schemas.microsoft.com/sharepoint/soap/ImportObjects" -> "ImportObjects",
    "http://schemas.microsoft.com/sharepoint/soap/GetObjectStatus" -> "GetObjectStatus",
    "http://schemas.microsoft.com/sharepoint/soap/GetObjectStatusCollection" -> "GetObjectStatusCollection",
    "http://schemas.microsoft.com/sharepoint/soap/GetObjectStatusCollectionWithExclusions" -> "GetObjectStatusCollectionWithExclusions",
    "http://schemas.microsoft.com/sharepoint/soap/Wait" -> "Wait",
    "http://schemas.microsoft.com/sharepoint/soap/GetFriendlyUrlBasedWebs" -> "GetFriendlyUrlBasedWebs",
    "http://schemas.microsoft.com/sharepoint/soap/GetChangedFriendlyUrlBasedWebs" -> "GetChangedFriendlyUrlBasedWebs",
    "http://schemas.microsoft.com/sharepoint/soap/GetAllFriendlyUrls" -> "GetAllFriendlyUrls",
    "http://schemas.microsoft.com/sharepoint/soap/GetChangedFriendlyUrls" -> "GetChangedFriendlyUrls",
    "http://schemas.microsoft.com/sharepoint/soap/GetFriendlyUrl" -> "GetFriendlyUrl",
    "http://schemas.microsoft.com/sharepoint/soap/GetAllContentSlices" -> "GetAllContentSlices"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where ((soapMsg.SoapAction as string) in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        this.SoapAction = soapMsg.SoapAction as string;
        this.ContainsFault = soapMsg.ContainsFault;
        
        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        return SoapActionsMap[this.SoapAction];
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}

