protocol MSSTWEB with
Documentation
{
    ProtocolName = "Windows Live SkyDrive Save to Web SOAP Web Service",
    ShortName = "MS-STWEB",
    DocumentName = "MS-STWEB",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-STWEB", Version = "3.3"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSSTWEB";

const map<string, string> WsaActionsMap =
{
    "GetWebAccountInfo" -> "GetWebAccountInfo",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetWebAccountInfoResponse" -> "GetWebAccountInfo",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetWebAccountInfoServerErrorFault" -> "GetWebAccountInfo",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetWebAccountInfoTermsOfUseNotSignedFault" -> "GetWebAccountInfo",
    "GetItemInfo" -> "GetItemInfo",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetItemInfoResponse" -> "GetItemInfo",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetItemInfoServerErrorFault" -> "GetItemInfo",
    "GetChangesSinceToken" -> "GetChangesSinceToken",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetChangesSinceTokenResponse" -> "GetChangesSinceToken",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetChangesSinceTokenServerErrorFault" -> "GetChangesSinceToken",
    "GetProductInfo" -> "GetProductInfo",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetProductInfoResponse" -> "GetProductInfo",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetProductInfoServerErrorFault" -> "GetProductInfo",
    "ResolveWebUrl" -> "ResolveWebUrl",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/ResolveWebUrlResponse" -> "ResolveWebUrl",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/ResolveWebUrlServerErrorFault" -> "ResolveWebUrl",
    "GetNotebooks" -> "GetNotebooks",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetNotebooksResponse" -> "GetNotebooks",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetNotebooksServerErrorFault" -> "GetNotebooks",
    "http://schemas.microsoft.com/clouddocuments/SkyDocsService/GetNotebooksTermsOfUseNotSignedFault" -> "GetNotebooks"
};

const map<string, string> SoapActionsMap = {
    "GetWebAccountInfo" -> "GetWebAccountInfo",
    "GetItemInfo" -> "GetItemInfo",
    "GetChangesSinceToken" -> "GetChangesSinceToken",
    "GetProductInfo" -> "GetProductInfo",
    "ResolveWebUrl" -> "ResolveWebUrl",
    "GetNotebooks" -> "GetNotebooks"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    optional string WsaAction;
    optional string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        if (soapMsg.WsaAction != "")
            this.WsaAction = soapMsg.WsaAction;
            
        if (soapMsg.SoapAction != "")
            this.SoapAction = soapMsg.SoapAction;
            
        this.ContainsFault = soapMsg.ContainsFault;

        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        if (this.WsaAction is wsaAcction:string)
            return WsaActionsMap[wsaAcction];
            
        if (this.SoapAction is soapAcction:string)
            return SoapActionsMap[soapAcction];
            
        return "Unknown";
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}
