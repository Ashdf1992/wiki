protocol MSCPSWS with
Documentation
{
    ProtocolName = "SharePoint Claim Provider Web Service Protocol",
    ShortName = "MS-CPSWS",
    DocumentName = "MS-CPSWS",
    ProtocolType = "SOAP",
    Description = ""
},
OPNAuthoring
{
    Copyright = "(c) 2014 Microsoft Corporation",
    References = 
        [
            new Reference {Name = "MS-CPSWS", Version = "3.0"},
        ],
    RevisionSummary =    
        [
            new Revision {Class=RevisionClass.Major, Version="1.0.0", Date="06/01/2013"},
        ]
};

using DSAD;
using SOAP;

const string ModuleName = "MSCPSWS";

const map<string, string> WsaActionsMap =
{
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ClaimTypes" -> "ClaimTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ClaimTypesResponse" -> "ClaimTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ClaimValueTypes" -> "ClaimValueTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ClaimValueTypesResponse" -> "ClaimValueTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/EntityTypes" -> "EntityTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/EntityTypesResponse" -> "EntityTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ProviderSchemas" -> "ProviderSchemas",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ProviderSchemasResponse" -> "ProviderSchemas",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/HierarchyProviderSchema" -> "HierarchyProviderSchema",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/HierarchyProviderSchemaResponse" -> "HierarchyProviderSchema",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/GetHierarchyAll" -> "GetHierarchyAll",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/GetHierarchyAllResponse" -> "GetHierarchyAll",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/GetHierarchy" -> "GetHierarchy",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/GetHierarchyResponse" -> "GetHierarchy",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/SearchAll" -> "SearchAll",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/SearchAllResponse" -> "SearchAll",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/Search" -> "Search",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/SearchResponse" -> "Search",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/Resolve" -> "Resolve",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveResponse" -> "Resolve",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveClaim" -> "ResolveClaim",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveClaimResponse" -> "ResolveClaim",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveMultiple" -> "ResolveMultiple",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveMultipleResponse" -> "ResolveMultiple",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveMultipleClaim" -> "ResolveMultipleClaim",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveMultipleClaimResponse" -> "ResolveMultipleClaim"
};

const map<string, string> SoapActionsMap = {
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ClaimTypes" -> "ClaimTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ClaimValueTypes" -> "ClaimValueTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/EntityTypes" -> "EntityTypes",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ProviderSchemas" -> "ProviderSchemas",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/HierarchyProviderSchema" -> "HierarchyProviderSchema",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/GetHierarchyAll" -> "GetHierarchyAll",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/GetHierarchy" -> "GetHierarchy",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/SearchAll" -> "SearchAll",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/Search" -> "Search",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/Resolve" -> "Resolve",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveClaim" -> "ResolveClaim",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveMultiple" -> "ResolveMultiple",
    "http://schemas.microsoft.com/sharepoint/claims/IClaimProviderWebService/ResolveMultipleClaim" -> "ResolveMultipleClaim"
};

endpoint Server over SOAP.Server accepts RequestMessage issues ResponseMessage;
client endpoint Client connected to Server;

autostart actor ActorOverSOAP(SOAP.Server soapServer) follows DSAD.ActorOverSOAP
{
    process soapServer accepts soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            RequestMessage msg = new RequestMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) accepts msg;
        }
    }
    
    process soapServer issues soapMsg:SOAP.SoapMessage where (soapMsg.WsaAction in WsaActionsMap || soapMsg.SoapAction in SoapActionsMap)
    {
        if (soapMsg#EndpointName != nothing && (soapMsg#EndpointName as string != ModuleName))
            reject;
        else
        {
            ResponseMessage msg = new ResponseMessage();
            msg.Initialize(soapMsg);
            dispatch (endpoint Server over soapServer) issues msg;
        }
    }
}

message MessageBase
{
    optional string WsaAction;
    optional string SoapAction;
    xml Body;
    bool ContainsFault;
    
    void Initialize(SOAP.SoapMessage soapMsg)
    {
        if (soapMsg.WsaAction != "")
            this.WsaAction = soapMsg.WsaAction;
            
        if (soapMsg.SoapAction != "")
            this.SoapAction = soapMsg.SoapAction;
            
        this.ContainsFault = soapMsg.ContainsFault;

        if (soapMsg.Body.ChildCount > 0)
        {
            this.Body = soapMsg.Body.Children[0];
        }
        else
        {
            this.Body = null;
        }
    }

    string GetOperationName()
    {
        if (this.WsaAction is wsaAcction:string)
            return WsaActionsMap[wsaAcction];
            
        if (this.SoapAction is soapAcction:string)
            return SoapActionsMap[soapAcction];
            
        return "Unknown";
    }
}

message RequestMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Request";
        }
        
        return this.GetOperationName() + " Request";
    }
}

message ResponseMessage : MessageBase
{
    override string ToString()
    {
        if (this.ContainsFault)
        {
            return this.GetOperationName() + " Fault Response";
        }
        
        return this.GetOperationName() + " Response";
    }
}
